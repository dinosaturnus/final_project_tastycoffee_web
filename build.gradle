plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
}

group 'com.dinosaturnus'
version '1.0-SNAPSHOT'

def selenideVersion = "6.16.1",
    junitVersion = "5.9.3",
    slf4jVersion = "2.0.7",
    allureSelenideVersion = "2.22.2",
    javaFakerVersion = "1.0.2";

allure {
    report {
        version.set("2.22.2")
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set("2.22.2")
            }
        }
    }
}

repositories {
    mavenCentral()
}

compileTestJava {
    options.encoding = 'UTF-8'
}

dependencies {
    testImplementation(
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "com.codeborne:selenide:$selenideVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "io.qameta.allure:allure-selenide:$allureSelenideVersion"
    )

    implementation(
            "com.github.javafaker:javafaker:$javaFakerVersion"
    )
}
tasks.withType(Test) {
    systemProperties(System.getProperties())
    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

test {
    useJUnitPlatform()
}

task smoke_test(type: Test) {
    useJUnitPlatform {
        includeTags("Smoke")
    }
}

task regress_test(type: Test) {
    useJUnitPlatform {
        includeTags("Regress")
    }
}
task parameterized_test(type: Test) {
    useJUnitPlatform {
        includeTags("Parameterized")
    }

}
